/* Common Sense */
xg/go(x/person())
to(xg,c/college())
->
possess(x,c)
;

xw/work(x/person())
on(xw,r/research())
->
do(x,r)
;

possess(x/person(),r/research())
->
do(x,r)
;

/* Default Content */
time(xg/go(x/person()),z/datetime())
to(xg,c/college())
->
l:<l/like(user,c) time(l,z)>
request_truth(emora,l)
;

time(pc/possess(user,c/college()),z/datetime())
pc{"confidence": 0}
user_aware(pc)
-> ask_if_like_college ->
l:<l/like(user,c) time(l,z)>
request_truth(emora,l)
;

time(pc/possess(user,c/college()),z/datetime())
pc{"confidence": 0}
user_aware(pc)
-> ask_college_property ->
p:<p/property(c) time(p,z)>
request(emora,p)
;

time(pc/possess(user,c/college()),z/datetime())
pc{"confidence": 0}
category(c)
user_aware(pc)
-> ask_college_type ->
op_more_info(c, pc)
request(emora, c)
;

time(prop_i/property(c/college()),z/datetime())
prop_i{"confidence": 0}
req/request(emora, prop_i)
user_aware(req)
-> ask_college_activity ->
pred:<like(user, pred/event(object())) in(pred,c) time(pred,z)>
request(emora, pred)
;

time(prop_i/property(c/college()),z/datetime())
prop_i{"confidence": 0}
user_aware(prop_i)
->
pred:<like(user, pred/event(object())) in(pred,c) time(pred,z)>
request(emora, pred)
;

like(user, pred/event(object()))
in(pred,c/college())
time(pred,z/datetime())
req/request(emora, pred)
user_aware(req)
->
ur/remind(user)
of(ur,m/memory())
type(m,group)
about(m,c)
;

um/major(user)
in(um,s/subject())
time(um,z/datetime())
user_aware(um)
->
l:<l/like(user,s) time(l,z)>
request_truth(emora,l)
;

time(l/like(user,s/subject()),z/datetime())
user_aware(l)
->
o:<cause(l,o/object())>
request(emora,o)
;

time(l/like(user,s/subject()),z/datetime())
user_aware(l)
->
ulx/learn(user,s)
time(enjoy(user,ulx),z)
;

/* More Specific Content */
l/like(user,c/college())
->
glad(emora,l)
fpc:<possess(user,p/part()) favorite(p) about(p,c) be(p,fpc/fav_part_college())>
request(emora,fpc)
;

l/like(user,c/college())
glad(emora,l)
possess(user,p/part())
favorite(p)
about(p,c)
be(p,fpc/fav_part_college())
req/request(emora,fpc)
req{"confidence": 0}
user_aware(req)
-> share_experience ->
have(emora,pr/peer())
type(pr,group)
be(pr,f/friend())
type(f,group)
;

time(do(user,research()),z/datetime())
->
busy(user)
ubr:<ubr/be(user,researcher())>
request_truth(emora,ubr)
;

ubr/be(user,r/researcher())
->
respect(emora,r)
;

ubr/be(user,r/researcher())
ubr{"confidence":-1}
->
emc/make(emora,c/contribution())
type(c,group)
to(emc,research())
;

time(study(x/person(),s/subject()),z/datetime())
->
xm:<time(xm/major(x),z) in(xm,s)>
request_truth(emora,xm)
;

time(xm/major(x/person()),z/datetime())
in(xm,s/subject())
xm{"confidence":-1}
->
o:<cause(xm,o/object())>
request(emora,o)
;

time(xm/major(x/person()),z/datetime())
in(xm,s/subject())
xm{"confidence":-1}
cause(xm,o/object())
req/request(emora,o)
user_aware(req)
->
understand(emora,xm)
em/major(emora)
in(em,biology())
not(em)
;

xs/study(x/person())
->
exciting(xs)
ej/jump(elephant())
not(ej)
el/learn(emora,ej)
time(el,past)
uke:<uke/know(user,ej) time(uke,past)>
request_truth(emora,uke)
;

xm/major(x/person())
in(xm,ss/social_science())
->
meaningful(ss)
;

xm/major(x/person())
in(xm,bm/business_major())
->
have(x,i/idea())
cool(i)
type(i,group)
innovative(s/solution())
type(s,group)

xei:<xei/enter(x,financial(id/industry()))>
request_truth(emora,xei)
;

enter(x/person(),fid/financial(industry()))
->
competitive(fid)
;

xei/enter(x/person(),fid/financial(industry()))
xei{"confidence":-1}
->
comfort(emora,x)
;

xm/major(x/person())
in(xm,ns/natural_science())
->
difficult(ns)
impressive(x)
;

xm/major(x/person())
in(xm,em/engineering_math())
->
ei/important(em)
to(ei,emora)
;

xm/major(x/person())
in(xm,em/literature_english())
->
admire(emora,x)
like(emora,em)
;

xm/major(x/person())
in(xm,a/arts_major())
->
cool(a)
ela/learn(emora,a)
ewe/want(emora,ela)
time(ewe,past)
;

xm/major(x/person())
in(xm,un/unknown_noun())
->
ek/know(emora)
about(ek,un)
d:<d/difficult(un)>
request_truth(emora,d)
;

xic/in(x/person(),c/college())
time(xic,now)
->
xt:<about(xt/tired(x),rc/remote(c))>
request_truth(emora,xt)
;

about(xt/tired(x/person()),rc/remote(c/college()))
->
open(c)
xr:<xr/return(x) xrc/to(xr,c) time(xr,future)>
request_truth(emora,xr)
;

about(xt/tired(x/person()),rc/remote(c/college()))
xt{"confidence":-1}
->
convenient(c)
comfortable(x)
;

xr/return(x/person())
to(xr,c/college())
->
see(x,p/peer())
type(p,group)
xs/study(x)
in(xs,l/library())
;

xr/return(x/person())
to(xr,c/college())
xr{"confidence":-1}
->
sad(x)
;

xic/in(x/person(),c/college())
time(xic,past)
->
p:<p/property(c)>
request(emora,p)
;

xg/go(x/person())
to(xg,c/college())
time(xg,now)
->
remember(emora,a/application())
type(a,group)
for(a,c)
cf:<cf/confident(x) for(cf,c)>
request_truth(emora,cf)
;

cf/confident(x/person())
for(cf,c/college())
->
s:<xm/major(x) in(xm,s/subject())>
request(emora,s)
;

cf/confident(x/person())
for(cf,c/college())
cf{"confidence":-1}
->
comfort(emora,x)
;

difficult(college())
->
agree(emora)
ekb/keep(emora,b/balance())
difficult(ekb)
;

sc/stressful(c/college())
->
sorry(emora,sc)
el/listen(emora)
to(el,user)
about(el,c)
time(el,always)
;

fun(c/college())
->
eh/happy(emora)
for(eh,user)
fp:<possess(user,p/part()) favorite(p) about(p,c) be(p,fp/fav_part_college())>
request(emora,fp)
;

different(c/college())
->
time(change(c),always)
;

time(busy(c/college(),z/datetime()))
->
ul/learn(user)
from(ul,c)
time(ul,z)
;

l/like(x/person(),pr/peer())
type(pr,group)
->
awesome(l)
;

be(x/person(),fm/freshman())
->
ebf/be(emora,fm)
time(ebf,past)
eac/attend(emora,c/club())
type(c,group)
time(eac,past)
;

be(x/person(),sm/sophomore())
->
ebs/be(emora,sm)
time(ebs,past)
eh/happy(emora)
as(eh,sm)
time(eh,past)
;

be(x/person(),jn/junior())
->
se/search(emora)
for(se,ip/internship())
type(ip,group)
time(se,past)
;

be(x/person(),sn/senior())
->
ip/important(g/graduation())
for(ip,sn)
type(sn,group)
;