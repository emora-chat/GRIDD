/* general activity */

time(e/event(user), past)
type(e, positive)
->
l:<l/like(user, e)>
request_truth(emora, l)
;


/* house purchase */

time(b/buy(user, h/house()), past)
-> user_bought_house ->
econgrat(emora, b)
;

user_aware(econgrat(emora, buy(user, h/house())))
->
time(look_forward(user, h), now)
;

user_aware(l/look_forward(user, h/house()))
->
epositive(emora, l)
;

l/look_forward(user, house())
eturn(l, 1)
la/large(yard())
uturn(la, 0)
-> large_yard ->
epositive(emora, la)
time(w/want(emora, y/yard()), now)
large(y)
lo/love(emora_dog, y)
would(lo)
cause(lo, w)
h:<time(h/have(user, pet()), now)>
request_truth(emora, h)
;


/* work */

time(w/work(user), past)
-> user_worked ->
t:<on(w, t/task())>
request(emora, t)

j2:<
    possess(user, j/job())
    best(j)
    be(j, j2/job())
>
request(emora, j2)
;

time(w/work(user), past)
on(w, t/assignment())
->
a:<for(t, a/authority())>
request(emora, a)
;

possess(user, j/job())
best(j)
be(j, j2/job())
_specific(j2)
->
X:<
    like(user, X/predicate(object()))
    as(X, j2)
>
request(emora, X)
;

l/like(user, help(user, person()))
->
eadmire(emora, l)
like(emora, help(emora, p2/person()))
type(p2, group)
;


