/* general activity */

time(e/event(user), past)
type(e, positive)
->
l:<l/like(user, e)>
request_truth(emora, l)
;

do(user,workout())
->
healthy(user)
he/hike(emora)
time(he,past)
;

gu/go(user)
to(gu,gym())
->
healthy(user)
he/hike(emora)
time(he,past)
;

gu/go(user)
for(gu,walk())
->
healthy(user)
he/hike(emora)
time(he,past)
;

time(ac/exercise(user),past)
type(ac,outdoor)
->
healthy(user)
he/hike(emora)
time(he,past)
;

/* take rest */

time(un/nap(user),past)
->
glad(emora,un)
;

nap()
->
ne/nap(emora)
love(emora,ne)
;

gu/go(user)
to(gu,bed())
time(gu,past)
->
time(nap(user),past)
;

gu/go(user)
su/nap(user)
to(gu,su)
time(gu,past)
->
time(nap(user),past)
;

/* house purchase */

time(b/buy(user, h/house()), past)
-> user_bought_house ->
econgrat(emora, b)
;

user_aware(econgrat(emora, buy(user, h/house())))
->
time(look_forward(user, h), now)
;

user_aware(l/look_forward(user, h/house()))
->
epositive(emora, l)
;

l/look_forward(user, house())
eturn(l, 1)
la/large(yard())
uturn(la, 0)
-> large_yard ->
epositive(emora, la)
time(w/want(emora, y/yard()), now)
large(y)
lo/love(nyla, y)
would(lo)
cause(lo, w)
h:<time(h/have(user, pet()), now)>
request_truth(emora, h)
;


/* work */

time(w/work(user), past)
-> user_worked ->
t:<on(w, t/task())>
request(emora, t)

j2:<
    possess(user, j/job())
    best(j)
    be(j, j2/job())
>
request(emora, j2)
;

time(w/work(user), past)
on(w, t/assignment())
->
a:<for(t, a/authority())>
request(emora, a)
;

possess(user, j/job())
best(j)
be(j, j2/job())
_specific(j2)
->
X:<
    like(user, X/predicate(object()))
    as(X, j2)
>
request(emora, X)
;

l/like(user, help(user, person()))
->
eadmire(emora, l)
like(emora, help(emora, p2/person()))
type(p2, group)
;

/* watch tv */

time(uwt/watch(user,t1/tv()),past)
-> user_watched_tv ->
h:<h/have(user,t2/thought()) type(t2,group) about(t2,t1)>
request_truth(emora,h)
;

h/have(user,t2/thought())
type(t2,group)
about(t2,t1/tv())
req/request_truth(emora,h)
user_aware(req)
->
appreciate(emora,h)
;

h/have(user,t2/thought())
type(t2,group)
about(t2,t1/tv())
not(h)
->
like(emora,t1)
traditional(t1)
;

/* talk to emora */

to(talk(user),emora)
->
possess(user,t/time())
appreciate(emora,t)
;

with(talk(user),emora)
->
to(talk(user),emora)
;

with(speak(user),emora)
->
to(talk(user),emora)
;

to(speak(user),emora)
->
to(talk(user),emora)
;

with(chat(user),emora)
->
to(talk(user),emora)
;

/* transportation */

time(urb/ride(user,bicycle()),past)
->
time(ub/bike(user),past)
;

time(ub/bike(user),past)
->
healthy(ub)
;

time(drive(user),past)
->
time(de/drive(emora),past)
boring(de)
;

time(de/drive(emora),past)
boring(de)
user_aware(de)
->
time(re/rent(emora,car()),past)
in(re,boston)
;

time(take(user,tr/trans_ob()),past)
_specific(tr)
->
utr:<utr/try(user,rt/RV_trip()) type(rt,group) time(utr,past)>
request_truth(emora,utr)
;

/* cleaning */

time(uc/clean(user),past)
->
organized(uc)
;

/* needs exercise */

time(s/sit(user),past)
->
need(user,exercise())
;

/* socializing */

ut/talk(user)
to(ut,p/person())
time(ut,past)
->
utc:<   mu/meet(user)
        with(fd/friend())
        type(fd,group)
        difficult(mu)
        c/cause(mu,pandemic())
        utc/think(user,c)   >
request_truth(emora, utc)
;

meet(user,person())
->
utc:<   mu/meet(user)
        with(fd/friend())
        type(fd,group)
        difficult(mu)
        c/cause(mu,pandemic())
        utc/think(user,c)   >
request_truth(emora, utc)
;

mu/meet(user)
with(fd/friend())
type(fd,group)
difficult(mu)
c/cause(mu,pandemic())
utc/think(user,c)
->
de/dislike(emora,vc/video_call())
type(vc,group)
;

mu/meet(user)
with(fd/friend())
type(fd,group)
difficult(mu)
c/cause(mu,pandemic())
utc/think(user,c)
not(utc)
->
de/dislike(emora,vc/video_call())
type(vc,group)
;

/* shopping */

time(shop(user),past)
->
se/shop(emora)
love(emora,se)
uhe:<uhe/have(user,e/experience()) similar(e)>
request_truth(emora,uhe)
;

uhe/have(user,e/experience())
similar(e)
->
be(emora,s/shopper())
slow(s)
;

uhe/have(user,e/experience())
similar(e)
not(uhe)
->
be(user,s/shopper())
fast(s)
;

/* bad feeling */

su/sick(user)
->
sorry(emora,su)
;

p/pain()
->
possess(user,p)
es/sorry(emora)
for(es,p)
;

/* meditation */

m/meditation()
->
good(m)
;

time(meditate(user),past)
->
good(meditation())
;

/* party */

pt/party()
->
ge/go(emora)
to(ge,ptt/party())
type(ptt,group)
time(ge,past)
;

/* having fun */

hu/happy(user)
->
o:<cause(hu,o/object())>
request(emora,o)
;

have(user,fun())
->
happy(user)
;

/* gardening */

time(garden(user),past)
->
possess(emora,m/mother())
time(garden(m),now)
;